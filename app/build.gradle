apply plugin: 'com.android.application'

ext {
    version_sdk = [
            compileSdk: 27,
            minSdk    : 14,
    ]
    version_lib = [
            appcompat  : "27.0.0",
            butterknife: "8.8.1",
    ]
}

android {
    compileSdkVersion version_sdk.compileSdk
    flavorDimensions "default"

    defaultConfig {
        minSdkVersion version_sdk.minSdk
        targetSdkVersion version_sdk.compileSdk
        buildConfigField "boolean", "IS_TEST_BUILD", "false"
        buildConfigField "boolean", "IS_GPLAY_BUILD", "false"
        buildConfigField("String[]", "APPLICATION_LANGUAGES", '{' + getUsedAndroidLanguages().collect {"\"${it}\""}.join(",")  + '}')
        buildConfigField "String", "GITHASH", "\"${getGitHash()}\""

        applicationId "io.github.froodyapp"
        versionName "1.0.10"
        versionCode 32

        buildConfigField "int", "ENTRY_LIFETIME_DAYS", "60"
        //vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
        flavorDefault {
        }

        flavorGplay {
            buildConfigField "boolean", "IS_GPLAY_BUILD", "true"
        }

        flavorTest {
            applicationId "io.github.froodyapp.test"
            versionCode = Integer.parseInt(new Date().format('yyMMdd'))
            versionName = new Date().format('yyMMdd')
            buildConfigField "boolean", "IS_TEST_BUILD", "true"
        }
    }

    lintOptions {
        warning 'InvalidPackage'
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    // Sub-Projects
    api(project(':froody-api')) {
        exclude module: "httpmime"
        exclude module: "httpcore"
        exclude module: "httpclient"
    }

    // Jars
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'

    // Android standard libs
    implementation "com.android.support:appcompat-v7:${version_lib.appcompat}"
    implementation "com.android.support:design:${version_lib.appcompat}"
    implementation "com.android.support:support-v4:${version_lib.appcompat}"
    implementation "com.android.support:cardview-v7:${version_lib.appcompat}"

    // Open Street Map & Geo
    implementation 'org.osmdroid:osmdroid-android:5.6.5'
    implementation 'com.github.MKergall:osmbonuspack:6.4'
    implementation 'ch.hsr:geohash:1.3.0'

    // Tool libraries
    implementation "com.jakewharton:butterknife:${version_lib.butterknife}"

    annotationProcessor "com.jakewharton:butterknife-compiler:${version_lib.butterknife}"
}
